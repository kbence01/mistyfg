plugins {
    id 'java-gradle-plugin'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.licenser' version '1.0.1'
    id 'com.github.ben-manes.versions' version '0.46.0'
}

version = '6.0.0-mistymc'
logger.lifecycle('Version: ' + version + ' Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

sourceSets {
    common
    mcp
    patcher
    userdev
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

configurations {
    configureEach {
        exclude group: 'org.ow2.asm'
    }
    mcpImplementation.extendsFrom commonImplementation
    patcherImplementation.extendsFrom commonImplementation
    userdevImplementation.extendsFrom mcpImplementation
    implementation.extendsFrom mcpImplementation, patcherImplementation, userdevImplementation
}

tasks.named('jar', Jar).configure {
    from sourceSets.common.output
    from sourceSets.mcp.output
    from sourceSets.patcher.output
    from sourceSets.userdev.output
}

tasks.named('sourcesJar', Jar).configure {
    from sourceSets.common.allJava
    from sourceSets.mcp.allJava
    from sourceSets.patcher.allJava
    from sourceSets.userdev.allJava
}

repositories {
    mavenLocal()
    maven { url = 'https://maven.minecraftforge.net/' }
}

license {
    header = file('HEADER')
    exclude '**/*.properties'
    exclude 'net/minecraftforge/gradle/common/diff/'
    exclude 'net/minecraftforge/gradle/common/util/JavaVersionParser.java'
}

tasks.named('wrapper', Wrapper).configure {
    gradleVersion = '8.4'
    distributionType = Wrapper.DistributionType.ALL
}

dependencies {
    commonImplementation gradleApi()
    commonImplementation 'commons-io:commons-io:2.11.0'
    commonImplementation 'com.google.code.gson:gson:2.10.1'
    commonImplementation 'com.google.guava:guava:31.1-jre'
    commonImplementation 'de.siegmar:fastcsv:2.2.1'
    commonImplementation('net.minecraftforge:artifactural:3.0.18') {
        transitive = false
    }
    commonImplementation('net.minecraftforge:unsafe:0.2.0') {
        transitive = false
    }
    commonImplementation 'org.apache.maven:maven-artifact:3.9.1'
    commonImplementation 'org.apache.httpcomponents:httpclient:4.5.14'
    commonImplementation 'net.minecraftforge:srgutils:0.5.10'
    commonImplementation 'net.minecraftforge:DiffPatch:2.0.12:all'

    mcpImplementation sourceSets.common.output
    patcherImplementation sourceSets.mcp.output
    patcherImplementation sourceSets.common.output
    userdevImplementation sourceSets.mcp.output
    userdevImplementation sourceSets.common.output

    userdevImplementation 'net.minecraftforge:JarJarMetadata:0.3.19'
    userdevImplementation 'net.minecraftforge:JarJarSelector:0.3.19'
}

gradlePlugin {
    plugins {
        mcp {
            id = 'net.minecraftforge.gradle.mcp'
            implementationClass = 'net.minecraftforge.gradle.mcp.MCPPlugin'
        }
        patcher {
            id = 'net.minecraftforge.gradle.patcher'
            implementationClass = 'net.minecraftforge.gradle.patcher.PatcherPlugin'
        }
        userdev {
            id = 'net.minecraftforge.gradle'
            implementationClass = 'net.minecraftforge.gradle.userdev.UserDevPlugin'
        }
    }
    automatedPublishing = true
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
        }
    }

    repositories {
        maven {
            name = "nexus"
            url = uri("https://nexus.mistymc.net/repository/maven-releases/")
            credentials {
                username = 'admin'
                password = '2aee098d-e79f-4288-ab34-8e8bb44600ef'
            }
        }
    }
}
